/*
 * Copyright (C) 2023 Kevin Buzeau
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
def billingKey
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
    billingKey = localProperties['billingPublicKey']
} else if (rootProject.hasProperty('billingPublicKey')) {
    billingKey = rootProject.billingPublicKey
}

android {
    namespace 'com.buzbuz.smartautoclicker.feature.billing'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.compileSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        kotlin {
            kotlinOptions {
                freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            }
        }
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Specifies one flavor dimension.
    flavorDimensions += "version"
    productFlavors {
        create("fDroid") {
            dimension = "version"
        }
        create("playStore") {
            dimension = "version"
            buildConfigField("String", "BILLING_PUBLIC_KEY", "\"" + billingKey + "\"")
            buildFeatures {
                viewBinding true
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"

    implementation project(path: ':core:ui')

    playStoreImplementation "androidx.appcompat:appcompat:${rootProject.ext.androidxAppCompatVersion}"
    playStoreImplementation "androidx.core:core-ktx:${rootProject.ext.androidxCoreVersion}"
    playStoreImplementation "androidx.fragment:fragment-ktx:${rootProject.androidxFragmentVersion}"
    playStoreImplementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${rootProject.ext.androidxLifecycleVersion}"

    playStoreImplementation "com.android.billingclient:billing:${rootProject.ext.billingVersion}"
    playStoreImplementation "com.android.billingclient:billing-ktx:${rootProject.ext.billingVersion}"

    playStoreImplementation "com.google.android.material:material:${rootProject.ext.googleMaterialVersion}"
}