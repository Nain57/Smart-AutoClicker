/*
 * Copyright (C) 2024 Kevin Buzeau
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
    alias(libs.plugins.googleKsp)
}

// Only apply gms/firebase plugins if we are building for the play store
def isPlayStoreBuild = getGradle().getStartParameter().getTaskRequests().toString().contains("PlayStore")
if (isPlayStoreBuild) {
    alias(libs.plugins.googleGms)
    alias(libs.plugins.googleCrashlytics)
}

// Read signing.properties file to find signing configuration
def signingProperties = new Properties()
def signingStoreFile = file("./smartautoclicker.jks")
def signingPropertiesFile = rootProject.file("signing.properties")
if (signingPropertiesFile.exists()) {
    signingProperties.load(new FileInputStream(signingPropertiesFile))
}

android {
    namespace = "com.buzbuz.smartautoclicker"
    compileSdk = libs.versions.androidCompileSdk.get() as Integer

    defaultConfig {
        applicationId = "com.buzbuz.smartautoclicker"
        minSdk = libs.versions.androidMinSdk.get() as Integer
        targetSdk = libs.versions.androidCompileSdk.get() as Integer
        versionCode = 41
        versionName = "2.4.2"

        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        kotlin {
            kotlinOptions {
                freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            }
        }

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    if (signingStoreFile.exists()) {
        signingConfigs {
            release {
                storeFile = signingStoreFile
                storePassword = signingProperties["signingStorePassword"] ?: rootProject.signingStorePassword
                keyAlias = signingProperties["signingKeyAlias"] ?: rootProject.signingKeyAlias
                keyPassword = signingProperties["signingKeyPassword"] ?: rootProject.signingKeyPassword
            }
        }
    }

    buildTypes {
        release {
            if (signingStoreFile.exists()) {
                signingConfig = signingConfigs.release
            }
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")

            if (isPlayStoreBuild) {
                firebaseCrashlytics {
                    nativeSymbolUploadEnabled = true
                    unstrippedNativeLibsDir = "build/intermediates/merged_native_libs/playStoreRelease/out/lib"
                }
            }
        }

        debug {
            applicationIdSuffix = ".debug"
        }
    }

    // Specifies one flavor dimension.
    flavorDimensions += "version"
    productFlavors {
        create("fDroid") {
            dimension = "version"
        }
        create("playStore") {
            dimension = "version"
        }
    }

    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        resources {
            excludes += ["META-INF/atomicfu.kotlin_module"]
        }
    }
}

dependencies {
    implementation(libs.kotlinx.coroutines.core)

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appCompat)
    implementation(libs.androidx.recyclerView)
    implementation(libs.androidx.fragment.ktx)

    implementation(libs.androidx.lifecycle.extensions)
    implementation(libs.androidx.lifecycle.viewmodel.ktx)
    implementation(libs.androidx.lifecycle.livedata.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.common.java8)

    implementation(libs.airbnb.lottie)
    implementation(libs.google.material)

    // Google Play Store version dependencies only
    playStoreImplementation(platform(libs.google.firebase.bom))
    playStoreImplementation(libs.google.firebase.crashlytics.ktx)
    playStoreImplementation(libs.google.firebase.crashlytics.ndk)

    implementation(project(path: ":core:base"))
    implementation(project(path: ":core:detection"))
    implementation(project(path: ":core:display"))
    implementation(project(path: ":core:domain"))
    implementation(project(path: ":core:dumb"))
    implementation(project(path: ":core:processing"))
    implementation(project(path: ":core:ui"))
    implementation(project(path: ":feature:backup"))
    implementation(project(path: ":feature:billing"))
    implementation(project(path: ":feature:floating-menu"))
    implementation(project(path: ":feature:scenario-config"))
    implementation(project(path: ":feature:scenario-config-dumb"))
    implementation(project(path: ":feature:scenario-debugging"))
    implementation(project(path: ":feature:tutorial"))
}

project.afterEvaluate {
    tasks.findAll { task ->
        task.name.startsWith("generateCrashlyticsSymbolFilePlayStoreRelease")
    }.forEach {task ->
        task.dependsOn("mergePlayStoreReleaseNativeLibs")
    }
}
